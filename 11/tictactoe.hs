-- p.139 - p.150
import Data.Char (isDigit)
import Data.List (transpose)
import System.IO (BufferMode (NoBuffering), hSetBuffering, stdout)

size :: Int
size = 3

-- | ã¾ã‚‹ã€ç©ºã€ã°ã¤
data Player = O | B | X deriving (Eq, Ord, Show)

-- 0 1 2
-- 3 4 5
-- 6 7 8
type Grid = [[Player]]

next :: Player -> Player
next O = X
next X = O
next B = B

-- | ç©ºã®ç›¤é¢ã‚’ä½œæˆã™ã‚‹ï¼ˆåˆæœŸçŠ¶æ…‹ï¼‰
empty :: Grid
empty = replicate size (replicate size B)

full :: Grid -> Bool
full = notElem B . concat

-- | æ¬¡ã®ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã‚’è¿”ã™
turn :: Grid -> Player
turn g = if os ps <= xs ps then O else X
  where
    ps = concat g
    os = length . filter (/= X)
    xs = length . filter (/= O)

-- | æ–œã‚ï¼ˆå·¦ä¸Š -> å³ä¸‹ï¼‰ã®ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã‚’è¿”ã™
-- ï¼ˆå³ä¸Š -> å·¦ä¸‹ï¼‰ã¯ç›¤é¢ã‚’åè»¢ã—ã¦ã‹ã‚‰é©ç”¨ã™ã‚Œã°å–å¾—ã§ãã‚‹
diag :: Grid -> [Player]
diag g = [g !! n !! n | n <- [0 .. size - 1]]

-- | æŒ‡å®šã—ãŸãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ãŒå‹åˆ©ã—ãŸã‹ã©ã†ã‹ã‚’è¿”ã™
wins :: Player -> Grid -> Bool
wins p g = any line (g ++ cols ++ dias)
  where
    line = all (== p)
    cols = transpose g
    dias = [diag g, diag (map reverse g)]

-- | ã©ã¡ã‚‰ã‹ã®ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ãŒå‹åˆ©ã—ãŸã‹ã©ã†ã‹ã‚’è¿”ã™
won :: Grid -> Bool
won g = wins O g || wins X g

showPlayer :: Player -> [String]
showPlayer O = ["   ", " O ", "   "]
showPlayer X = ["   ", " X ", "   "]
showPlayer B = ["   ", "   ", "   "]

-- | ãƒªã‚¹ãƒˆã®å„è¦ç´ é–“ã«å€¤ã‚’å·®ã—è¾¼ã‚€
interleave :: a -> [a] -> [a]
interleave x [] = []
interleave x [y] = [y]
interleave x (y : ys) = y : x : interleave x ys

showRow :: [Player] -> [String]
showRow = beside . interleave bar . map showPlayer
  where
    beside = foldr1 (zipWith (++))
    bar = replicate 3 "|"

putGrid :: Grid -> IO ()
putGrid = putStrLn . unlines . concat . interleave bar . map showRow
  where
    bar = [replicate ((size * 4) - 1) '-']

-- | ãƒã‚¹ã‚’ã¨ã‚‹ã“ã¨ãŒã§ãã‚‹ã‹ã‚’è¿”ã™
valid :: Grid -> Int -> Bool
valid g i = 0 <= i && i < size ^ 2 && concat g !! i == B

chop :: Int -> [a] -> [[a]]
chop n [] = []
chop n xs = take n xs : chop n (drop n xs)

-- | æŒ‡å®šã®ãƒã‚¹ã‚’ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã®ãƒã‚¹ã«ã™ã‚‹
move :: Grid -> Int -> Player -> [Grid]
move g i p = [chop size (xs ++ [p] ++ ys) | valid g i]
  where
    (xs, B : ys) = splitAt i (concat g)

getNat :: String -> IO Int
getNat prompt = do
  putStr prompt
  xs <- getLine
  if xs /= [] && all isDigit xs
    then return (read xs)
    else do
      putStrLn "ERROR: Invalid number ğŸ¤¯"
      getNat prompt

-- | ç”»é¢ã®ã‚¯ãƒªã‚¢
cls :: IO ()
cls = putStr "\ESC[2J"

-- | æŒ‡å®šã®ä½ç½®ã«ã‚«ãƒ¼ã‚½ãƒ«ã‚’æŒã£ã¦ã„ã
goto :: (Int, Int) -> IO ()
goto (x, y) = putStr ("\ESC[" ++ show y ++ ";" ++ show x ++ "H")

prompt :: Player -> String
prompt p = "Player " ++ show p ++ ", enter your move: "

run :: Grid -> Player -> IO ()
run g p = do
  cls
  goto (1, 1)
  putGrid g
  run' g p

run' :: Grid -> Player -> IO ()
run' g p
  | wins O g = putStrLn "ğŸ‰ Player O wins!\n"
  | wins X g = putStrLn "ğŸ‰ Player X wins!\n"
  | full g = putStrLn "ğŸ™ It's a draw!\n"
  | otherwise = do
      i <- getNat (prompt p)
      case move g i p of
        [] -> do
          putStrLn "ERROR: Invalid move ğŸ¤¯"
          run' g p
        [g'] -> run g' (next p)
        _ -> putStrLn "ERROR: Exception game ğŸ”¥"

tictactoe :: IO ()
tictactoe = run empty O

data Tree a = Node a [Tree a] deriving (Show)

-- | æ¬¡ã«é·ç§»ã§ãã‚‹ãƒã‚¹ã®æ§‹é€ ã®ãƒªã‚¹ãƒˆã‚’è¿”ã™
moves :: Grid -> Player -> [Grid]
moves g p
  | won g = []
  | full g = []
  | otherwise = concat [move g i p | i <- [0 .. ((size ^ 2) - 1)]]

-- | ã¨ã‚Šã†ã‚‹å…¨ã¦ã®ãƒã‚¹é…ç½®æœ¨æ§‹é€ ã‚’è¿”ã™
gametree :: Grid -> Player -> Tree Grid
gametree g p = Node g [gametree g' (next p) | g' <- moves g p]

-- | ä¸ãˆã‚‰ã‚ŒãŸæ·±ã•ä»¥é™ã® Node ã‚’å‰Šé™¤ã™ã‚‹
prune :: Int -> Tree a -> Tree a
prune 0 (Node x _) = Node x []
prune n (Node x ts) = Node x [prune (n - 1) t | t <- ts]

-- | æœ¨æ§‹é€ ã®æœ€å¤§ã®æ·±ã•
depth :: Int
depth = 9

-- | ç›¤é¢ã®æœ¨æ§‹é€ ã®å„ Node ã«ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ã®ãƒ©ãƒ™ãƒ«ã‚’ä»˜ä¸ã™ã‚‹
minimax :: Tree Grid -> Tree (Grid, Player)
minimax (Node g []) -- è‘‰ã®å ´åˆã¯ãã®æ™‚ç‚¹ã®å‹è€…ã‚’ãƒ©ãƒ™ãƒ«ã«è¨­å®šã™ã‚‹ã€ã„ãªã„å ´åˆã¯ç©ºã‚’ãƒ©ãƒ™ãƒ«ã«è¨­å®šã™ã‚‹
  | wins O g = Node (g, O) []
  | wins X g = Node (g, X) []
  | otherwise = Node (g, B) []
minimax (Node g ts) -- ç¯€ã®å ´åˆã¯ï¼‘éšå±¤ä¸‹ã®å­ã®ãƒ©ãƒ™ãƒ«ã«å¯¾ã—ã¦ã€ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼ãŒ O ãªã‚‰ã®æœ€å°å€¤ã‚’ã€X ã®å ´åˆã¯æœ€å¤§å€¤ã‚’ãƒ©ãƒ™ãƒ«ã«è¨­å®šã™ã‚‹
  | turn g == O = Node (g, minimum ps) ts'
  | turn g == X = Node (g, maximum ps) ts'
  where
    ts' = map minimax ts
    ps = [p | Node ((_, p)) _ <- ts']

-- | ä¸ãˆã‚‰ã‚ŒãŸæœ¨æ§‹é€ ã®æ ¹ã¨åŒã˜ãƒ©ãƒ™ãƒ«ã® Node ãŒæœ€å–„æ‰‹ã¨ãªã‚‹
bestmove :: Grid -> Player -> Grid
bestmove g p = head [g' | Node (g', p') _ <- ts, p' == best]
  where
    tree = prune depth (gametree g p)
    Node (_, best) ts = minimax tree -- ts ã¯ï¼‘éšå±¤ã—ãŸã® Node ã®ãƒªã‚¹ãƒˆ

play :: Grid -> Player -> IO ()
play g p = do
  cls
  goto (1, 1)
  putGrid g
  play' g p

play' :: Grid -> Player -> IO ()
play' g p
  | wins O g = putStrLn "ğŸ‰ Player O wins!\n"
  | wins X g = putStrLn "ğŸ‰ Player X wins!\n"
  | full g = putStrLn "ğŸ™ It's a draw!\n"
  | p == O = do
      i <- getNat (prompt p)
      case move g i p of
        [] -> do
          putStrLn "ERROR: Invalid move ğŸ¤¯"
          play' g p
        [g'] -> play g' (next p)
        _ -> putStrLn "ERROR: Exception game ğŸ”¥"
  | p == X = do
      putStr "Player X is thinking..."
      (play $! (bestmove g p)) (next p)

main :: IO ()
main = do
  hSetBuffering stdout NoBuffering
  play empty O
